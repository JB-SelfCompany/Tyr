plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'com.jbselfcompany.tyr'
    compileSdk 36

    dependenciesInfo {
        includeInApk = false
        includeInBundle = false
    }

    defaultConfig {
        applicationId "com.jbselfcompany.tyr"
        minSdk 23
        targetSdk 33
        versionCode 12
        versionName "1.4.2"
        resourceConfigurations += ['en', 'ru']
        ndk {
            abiFilters 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a', 'x86', 'x86_64'
            universalApk true
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    packagingOptions {
        jniLibs {
            useLegacyPackaging false
        }
        resources {
            excludes += [
                    'org/bouncycastle/pqc/crypto/picnic/*.*',
                    'META-INF/*.version',
                    'META-INF/*.SF',
                    'META-INF/*.RSA',
                    'META-INF/*.DSA',
                    'META-INF/LICENSE*',
                    'META-INF/**/LICENSE*',
                    'META-INF/NOTICE*',
                    'META-INF/DEPENDENCIES'
            ]
        }
    }

    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def abiName = output.getFilter(com.android.build.OutputFile.ABI) ?: "universal"
            def formattedDate = new Date().format('yyyyMMdd')
            outputFileName = "Tyr-${variant.versionName}-${abiName}.apk"
        }
    }
}

dependencies {
    // Yggmail library
    implementation fileTree(include: ['*.aar'], dir: 'libs')

    // AndroidX libraries
    implementation 'androidx.core:core-ktx:1.17.0'
    implementation 'androidx.appcompat:appcompat:1.7.1'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.2.1'
    implementation 'com.google.android.material:material:1.13.0'
    implementation 'androidx.viewpager2:viewpager2:1.1.0'

    // Security - Encrypted SharedPreferences
    implementation 'androidx.security:security-crypto:1.1.0-alpha06'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.7.0'
}